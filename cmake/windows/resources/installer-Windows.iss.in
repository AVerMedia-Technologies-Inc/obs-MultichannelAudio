#define MyAppName "@PLUGIN_DISPLAY_NAME@"
#define MyAppVersion "@CMAKE_PROJECT_VERSION@"
#define MyAppPublisher "@PLUGIN_AUTHOR@"
#define MyAppURL "@PLUGIN_WEBSITE@"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{@UUID_APP@}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={code:GetDirName}
DefaultGroupName={#MyAppName}
OutputBaseFilename={#MyAppName}-{#MyAppVersion}-Windows-Installer
Compression=lzma
SolidCompression=yes
DirExistsWarning=no
SetupLogging=yes
PrivilegesRequired=admin
AppCopyright=Copyright (C) 2024 AVerMedia Technologies, Inc.
WizardStyle=classic
VersionInfoVersion={#MyAppVersion}

[Languages]
Name: "en_US"; MessagesFile: "compiler:Default.isl,..\i18n\en_US.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "zh_TW"; MessagesFile: "compiler:Languages\ChineseTraditional.isl,..\i18n\zh_TW.isl"; LicenseFile: "..\i18n\zh-Hant.lproj\LICENSE.txt";
;Name: "cs_CZ"; MessagesFile: "compiler:Languages\Czech.isl,..\i18n\cs_CZ.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "de_DE"; MessagesFile: "compiler:Languages\German.isl,..\i18n\de_DE.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "es_ES"; MessagesFile: "compiler:Languages\Spanish.isl,..\i18n\es_ES.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
;Name: "it_IT"; MessagesFile: "compiler:Languages\Italian.isl,..\i18n\it_IT.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "ru_RU"; MessagesFile: "compiler:Languages\Russian.isl,..\i18n\ru_RU.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "fr_FR"; MessagesFile: "compiler:Languages\French.isl,..\i18n\fr_FR.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "ja_JP"; MessagesFile: "compiler:Languages\Japanese.isl,..\i18n\ja_JP.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "ko_KR"; MessagesFile: "compiler:Languages\Korean.isl,..\i18n\ko_KR.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "zh_CN"; MessagesFile: "compiler:Languages\ChineseSimplified.isl,..\i18n\zh_CN.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
;Name: "pl_PL"; MessagesFile: "compiler:Languages\Polish.isl,..\i18n\pl_PL.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
;Name: "nl_NL"; MessagesFile: "compiler:Languages\Dutch.isl,..\i18n\nl_NL.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
Name: "th_TH"; MessagesFile: "compiler:Languages\Thai.isl,..\i18n\th_TH.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";
;Name: "pt_BR"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl,..\i18n\pt_BR.isl"; LicenseFile: "..\i18n\en.lproj\LICENSE.txt";

[Files]
Source: "..\release\Package\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\LICENSE"; Flags: dontcopy
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Registry]
; write to the registry on a 64-bit machine
; https://stackoverflow.com/a/12958439
Root: HKLM64; Subkey: "Software\AVerMedia"; Flags: createvalueifdoesntexist; Check: IsWin64
Root: HKLM64; Subkey: "Software\AVerMedia\OBS_Plugins\@CMAKE_PROJECT_NAME@"; Flags: createvalueifdoesntexist uninsdeletekey; Check: IsWin64
Root: HKLM64; Subkey: "Software\AVerMedia\OBS_Plugins\@CMAKE_PROJECT_NAME@"; ValueType: string; ValueName: "Path"; ValueData: "{app}"; Check: IsWin64
Root: HKLM64; Subkey: "Software\AVerMedia\OBS_Plugins\@CMAKE_PROJECT_NAME@"; ValueType: string; ValueName: "Language"; ValueData: "{language}"; Check: IsWin64
Root: HKLM64; Subkey: "Software\AVerMedia\OBS_Plugins\@CMAKE_PROJECT_NAME@"; ValueType: string; ValueName: "Uninstaller"; ValueData: "{uninstallexe}"; Check: IsWin64
Root: HKLM64; Subkey: "Software\AVerMedia\OBS_Plugins\@CMAKE_PROJECT_NAME@"; ValueType: string; ValueName: "Version"; ValueData: "{#MyAppVersion}"; Check: IsWin64

[Code]
procedure InitializeWizard();
var
  GPLText: AnsiString;
  Page: TOutputMsgMemoWizardPage;
begin
  // OBS GPL license in Welcome page
  ExtractTemporaryFile('LICENSE');
  LoadStringFromFile(ExpandConstant('{tmp}\LICENSE'), GPLText);
  Page := CreateOutputMsgMemoPage(wpWelcome,
    ExpandConstant('{cm:GPL_LicenseInformation}'), ExpandConstant('{cm:GPL_ReviewLicense,{#MyAppName}}'),
    ExpandConstant('{cm:GPL_LicenseInfoDesc}'),
    String(GPLText)
  );

  // How to change default choice in License agreement in INNO Setup
  // https://stackoverflow.com/a/51291566/21177210
  WizardForm.LicenseAcceptedRadio.Checked := True;
end;

// credit where it's due :
// following function come from https://github.com/Xaymar/obs-studio_amf-encoder-plugin/blob/master/%23Resources/Installer.in.iss#L45
function GetDirName(Value: string): string;
var
  InstallPath: string;
begin
  // initialize default path, which will be returned when the following registry
  // key queries fail due to missing keys or for some different reason
  Result := '{autopf}\obs-studio';
  // query the first registry value; if this succeeds, return the obtained value
  if RegQueryStringValue(HKLM32, 'SOFTWARE\OBS Studio', '', InstallPath) then
    Result := InstallPath
end;

